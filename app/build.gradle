apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.sudansh.pulse.testing.OpenClass'
}

android {
    compileSdkVersion 28
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.sudansh.carbooking"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    supportLibraryVersion = "28.0.0-rc01"
    lifecycle = "1.1.1"
    retrofit = '2.4.0'
    room = '1.1.1'
    espresso = '3.0.1'
    mockito = '2.7.19'
    koin_version = '0.9.3'
    stetho = '1.5.0'
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"

    //maps
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Constraint Layout
    implementation "com.android.support.constraint:constraint-layout:1.1.3"

    // LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:$lifecycle"
    testImplementation "android.arch.core:core-testing:$lifecycle"
    androidTestImplementation "android.arch.core:core-testing:$lifecycle"

    // Room
    implementation "android.arch.persistence.room:runtime:$room"
    kapt "android.arch.persistence.room:compiler:$room"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"

    //Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-architecture:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"

    //stetho
    implementation "com.facebook.stetho:stetho:$stetho"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho"

    //mockito
    testImplementation "org.mockito:mockito-core:$mockito"
    androidTestImplementation "org.mockito:mockito-android:$mockito"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
    testImplementation 'junit:junit:4.12'

    //espresso
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'
}
